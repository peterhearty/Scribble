apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    // See http://tools.android.com/tech-docs/unit-testing-support.
    // Otherwise get a "Not mocked" exception.
    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "uk.org.platitudes.scribble"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "0.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // From http://stackoverflow.com/questions/28249036/app-release-apk-how-to-change-this-default-generated-apk-name
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def formattedDate = new Date().format('yyyyMMddHH')
                    def newName = output.outputFile.name
                    newName = newName.replace("app-", "uk.org.platitudes.scribble")
                    newName = newName.replace("release", "-v" + defaultConfig.versionName + "-" + formattedDate)
                    //noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }

        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    // https://developers.google.com/android/guides/setup
    compile 'com.google.android.gms:play-services-drive:7.5.0'
    // These are Maven Central coordinates, see for example https://search.maven.org/#search|ga|1|robolectric
    // Libraries get downloaded and cached in ~/.gradle/caches
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
//    testCompile "org.robolectric:robolectric:3.0"
//    testCompile "org.robolectric:shadows-play-services:3.0"
}
